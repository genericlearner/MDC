START -> PROG
PROG -> CLASSIMPLFUNC PROG | EPSILON
CLASSIMPLFUNC -> CLASSDECL | IMPLDEF | FUNCDEF
CLASSDECL -> class id ISA1 lbrace VISMEMBERDECL rbrace semicolon
VISMEMBERDECL -> VISIBILITY MEMDECL VISMEMBERDECL | EPSILON
ISA1 -> isa id ISA2 | EPSILON
ISA2 -> comma id ISA2 | EPSILON
IMPLDEF -> implementation id lbrace IMPLBODY rbrace
IMPLBODY -> FUNCDEF IMPLBODY | EPSILON
FUNCDEF -> FUNCHEAD FUNCBODY
VISIBILITY -> public | private
MEMDECL -> FUNCDECL | ATTRDECL
FUNCDECL -> FUNCHEAD semicolon
FUNCHEAD -> function id lparen FPARAMS rparen arrow RETURNTYPE
| constructor lparen FPARAMS rparen
FUNCBODY -> lbrace LOCALVARDECLORSTAT2 rbrace
LOCALVARDECLORSTAT -> LOCALVARDECL | STATEMENT
LOCALVARDECLORSTAT2 -> LOCALVARDECLORSTAT LOCALVARDECLORSTAT2 | EPSILON
ATTRDECL -> attribute VARDECL
LOCALVARDECL -> local VARDECL
VARDECL -> id colon TYPE ARRAYSIZES semicolon
STATEMENT -> FUNCALLORASSIGN semicolon
| if lparen RELEXPR rparen then STATBLOCK else STATBLOCK semicolon
| while lparen RELEXPR rparen STATBLOCK semicolon
| read lparen VARIABLE rparen semicolon
| write lparen EXPR rparen semicolon
| return lparen EXPR rparen semicolon

FUNCALLORASSIGN -> IDORSELF FUNCALLORASSIGN2
FUNCALLORASSIGN2 -> INDICES FUNCALLORASSIGN3 | lparen APARAMS rparen FUNCALLORASSIGN4
FUNCALLORASSIGN3 -> ASSIGNOP EXPR | dot id FUNCALLORASSIGN2
FUNCALLORASSIGN4 -> dot id FUNCALLORASSIGN2 | EPSILON
STATBLOCK -> lbrace STATEMENTS rbrace | STATEMENT | EPSILON
STATEMENTS -> STATEMENT STATEMENTS | EPSILON
EXPR -> ARITHEXPR EXPR2
EXPR2 -> RELOP ARITHEXPR | EPSILON
RELEXPR -> ARITHEXPR RELOP ARITHEXPR
ARITHEXPR -> TERM RIGHTRECARITHEXPR
RIGHTRECARITHEXPR -> ADDOP TERM RIGHTRECARITHEXPR | EPSILON
SIGN -> plus | minus
TERM -> FACTOR RIGHTRECTERM
RIGHTRECTERM -> MULTOP FACTOR RIGHTRECTERM | EPSILON
FACTOR -> IDORSELF FACTOR2 REPTVARIABLEORFUNCTIONCALL
| floatlit
| intlit
| lparen ARITHEXPR rparen
| not FACTOR
| SIGN FACTOR
FACTOR2 -> lparen APARAMS rparen | INDICES
INDICES -> INDICE INDICES | EPSILON
REPTVARIABLEORFUNCTIONCALL -> IDNEST REPTVARIABLEORFUNCTIONCALL | EPSILON
VARIABLE -> IDORSELF VARIABLE2
VARIABLE2 -> INDICES REPTVARIABLE | lparen APARAMS rparen VARIDNEST
REPTVARIABLE -> VARIDNEST REPTVARIABLE | EPSILON
VARIDNEST -> dot id VARIDNEST2
VARIDNEST2 -> lparen APARAMS rparen VARIDNEST | INDICES
INDICE -> lsqbr ARITHEXPR rsqbr
IDNEST -> dot id IDNEST2
IDNEST2 -> lparen APARAMS rparen | INDICES
ARRAYSIZE -> lsqbr ARRAYSIZE2
ARRAYSIZE2 -> intlit rsqbr | rsqbr
ARRAYSIZES -> ARRAYSIZE ARRAYSIZES | EPSILON
TYPE -> int | float | id
RETURNTYPE -> TYPE | void
APARAMS -> EXPR REPTAPARAMS1 | EPSILON
REPTAPARAMS1 -> APARAMSTAIL REPTAPARAMS1 | EPSILON
APARAMSTAIL -> comma EXPR
FPARAMS -> id colon TYPE ARRAYSIZES REPTFPARAMS1 | EPSILON
REPTFPARAMS1 -> FPARAMSTAIL REPTFPARAMS1 | EPSILON
FPARAMSTAIL -> comma id colon TYPE ARRAYSIZES
ASSIGNOP -> assign
RELOP -> eq | neq | lt | gt | lteq | gteq
ADDOP -> plus | minus | or
MULTOP -> * | / | and
IDORSELF -> id | self
